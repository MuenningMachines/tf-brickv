!include "WinVer.nsh"
!include "Sections.nsh"
!include "x64.nsh"

Function InstallUpgradeDriver

  Pop $R0 ; .inf path
  Pop $R1 ; driver name

  DetailPrint 'pnputil.exe /add-driver "$R0" /install'
  DetailPrint "Installing $R1 driver..."
  ${DisableX64FSRedirection}
  nsExec::ExecToLog 'pnputil.exe /add-driver "$R0" /install'
  ${EnableX64FSRedirection}
  Pop $0
  StrCmp $0 "error" lbl_not_found
  StrCmp $0 "timeout" lbl_timeout
  IntCmp $0 "1" lbl_already_exists
  IntCmp $0 0 lbl_done
  DetailPrint "pnputil.exe reported an error: $0"
  Goto lbl_nodriver

lbl_not_found:
  DetailPrint "pnputil.exe not found"
  Goto lbl_nodriver

lbl_timeout:
  DetailPrint "Timeout waiting for pnputil.exe to run"
  Goto lbl_nodriver

lbl_already_exists:
  DetailPrint "Driver $R1 is already up-to-date"
  Goto lbl_nodriver

lbl_nodriver:
lbl_done:

FunctionEnd



Name "Brick Viewer <<VERSION>>"

OutFile "brickv_windows_<<UNDERSCORE_VERSION>>.exe"

XPStyle on

; The default installation directory
InstallDir "$PROGRAMFILES\Tinkerforge\Brickv"

; Registry key to check for directory (so if you install again, it will
; overwrite the old one automatically)
InstallDirRegKey HKLM "Software\Tinkerforge\Brickv" "Install_Dir"

; Request application privileges for Windows Vista
RequestExecutionLevel admin

; Make Windows 8.1 and newer not lie about its version number
ManifestSupportedOS all

;--------------------------------

!define BRICKV_VERSION <<VERSION>>

;--------------------------------

!macro macrouninstall

  DetailPrint "Uninstall Brick Viewer..."

  RMDir /R "$INSTDIR"

  ; Remove current user (for backward compatibility) and all users menu shortcuts
  SetShellVarContext current
  Delete "$SMPROGRAMS\Tinkerforge\Brickv *.lnk"
  RMDir "$SMPROGRAMS\Tinkerforge"
  SetShellVarContext all
  Delete "$SMPROGRAMS\Tinkerforge\Brickv *.lnk"
  RMDir "$SMPROGRAMS\Tinkerforge"
  SetShellVarContext current

  ; Remove registry keys
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Brickv" ; Remove old key too
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Tinkerforge Brickv"
  DeleteRegKey HKLM "Software\Tinkerforge\Brickv"

!macroend

; Pages

Page components
Page directory
Page instfiles

UninstPage uninstConfirm
UninstPage instfiles

;--------------------------------

Section /o "-Uninstall Brick Viewer" SEC_UNINSTALL_OLD

  !insertmacro macrouninstall

SectionEnd

;--------------------------------

; The stuff to install
Section "Install Brick Viewer ${BRICKV_VERSION}"
  SectionIn RO

  DetailPrint "Install Brick Viewer..."

  SetOutPath "$INSTDIR"
  File /r /x nsis ..\*

  ; Write the installation path into the registry
  WriteRegStr HKLM "Software\Tinkerforge\Brickv" "Install_Dir" "$INSTDIR"
  WriteRegStr HKLM "Software\Tinkerforge\Brickv" "Version" ${BRICKV_VERSION}

  ; Write the uninstall keys for Windows
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Tinkerforge Brickv" "DisplayName" "Tinkerforge Brick Viewer ${BRICKV_VERSION}"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Tinkerforge Brickv" "DisplayIcon" '"$INSTDIR\brickv.exe",0'
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Tinkerforge Brickv" "DisplayVersion" "${BRICKV_VERSION}"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Tinkerforge Brickv" "Publisher" "Tinkerforge GmbH"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Tinkerforge Brickv" "UninstallString" '"$INSTDIR\uninstall.exe"'
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Tinkerforge Brickv" "NoModify" 1
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Tinkerforge Brickv" "NoRepair" 1
  WriteUninstaller "uninstall.exe"

  ; Create start menu shortcut for all users
  SetOutPath $INSTDIR\ ; set working directory for brickv.exe
  SetShellVarContext all
  createDirectory "$SMPROGRAMS\Tinkerforge"
  createShortCut "$SMPROGRAMS\Tinkerforge\Brickv ${BRICKV_VERSION}.lnk" "$INSTDIR\brickv.exe"
  SetShellVarContext current

SectionEnd

;--------------------------------

Section /o "-Install/Update Brick Bootloader Driver" SEC_INSTALL_BRICK_BOOTLOADER_DRIVER

  Push "Brick Bootloader"
  Push "$INSTDIR\drivers\bootloader\atm6124_cdc.inf"
  Call InstallUpgradeDriver

SectionEnd

;--------------------------------

Section "Install/Update ESP32 Bootloader Driver"

${If} ${AtLeastWin10}

  ; Windows 10/11
  Push "ESP32 Bootloader"
  Push "$INSTDIR\drivers\esp32\win10\silabser.inf"
  Call InstallUpgradeDriver

${Else}

  ; Windows 7/8/8.1
  Push "ESP32 Bootloader"
  Push "$INSTDIR\drivers\esp32\win7\slabvcp.inf"
  Call InstallUpgradeDriver

${EndIf}

SectionEnd

;--------------------------------

Function .onInit

${IfNot} ${AtLeastWin7}

  MessageBox MB_OK|MB_ICONEXCLAMATION "Brick Viewer ${BRICKV_VERSION} requires Windows 7 or newer!"
  Quit

${EndIf}

  ; Check to see if already installed
  ClearErrors
  ReadRegStr $0 HKLM "Software\Tinkerforge\Brickv" "Version"
  IfErrors not_installed ; Version not set

  SectionSetText ${SEC_UNINSTALL_OLD} "Uninstall Brick Viewer $0" ; make item visible
  IntOp $0 ${SF_SELECTED} | ${SF_RO}
  SectionSetFlags ${SEC_UNINSTALL_OLD} $0

not_installed:

${IfNot} ${AtLeastWin8.1}

  ; Install Brick bootloader driver only on systems < Windows 8.1
  ; Windows >= 8.1 comes with a universal usbser.sys driver
  SectionSetText ${SEC_INSTALL_BRICK_BOOTLOADER_DRIVER} "Install/Update Brick Bootloader Driver" ; make item visible
  SectionSetFlags ${SEC_INSTALL_BRICK_BOOTLOADER_DRIVER} ${SF_SELECTED}

${EndIf}

FunctionEnd

;--------------------------------
; Uninstaller

Section "Uninstall"

  !insertmacro macrouninstall

SectionEnd
